/*
 * Copyright 2011 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package org.drools.informer.presentation;

import org.drools.informer.*;
import org.drools.informer.Question.QuestionType;
import org.drools.informer.MultipleChoiceQuestion.PossibleAnswer;


rule "Recomm_Default"
salience 10
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $q : Question( $qid : id, $qLab : preLabel, $req : required,
                    $type : answerType == Question.QuestionType.TYPE_TEXT || == Question.QuestionType.TYPE_NUMBER
                    || == Question.QuestionType.TYPE_DECIMAL )
    $qga : QuestionGUIAdapter( itemId == $qid, suggestedControl == null )
then
    $qga.setSuggestedControl("text");
end


rule "Recomm_Date"
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $q : Question( $qid : id, $qLab : preLabel, $req : required, $type : answerType == Question.QuestionType.TYPE_DATE )
    $qga : QuestionGUIAdapter( itemId == $qid )
then
    $qga.setSuggestedControl("date");
end


rule "Recomm_Bool"
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions )
    $q : Question( $qid : id, $qLab : preLabel, $req : required, $type : answerType == Question.QuestionType.TYPE_BOOLEAN )
    $qga : QuestionGUIAdapter( itemId == $qid, suggestedControl == null )
then
    $qga.setSuggestedControl("button");
end



rule "Recomm_Radio"
dialect "mvel"
salience -1  //override
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $q : MultipleChoiceQuestion( $qid : id, $qLab : preLabel, $req : required, $type : answerType,
                   singleAnswer == true,
                   numOfPossibleAnswers > 0 && numOfPossibleAnswers < 6 )
    $qga : QuestionGUIAdapter( itemId == $qid )
then
    $qga.setSuggestedControl("radio");
end


rule "Recomm_Check"
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $q : MultipleChoiceQuestion( $qid : id, $qLab : preLabel, $req : required, $type : answerType,
                   singleAnswer == false,
                   numOfPossibleAnswers > 0 && numOfPossibleAnswers < 6 )
    $qga : QuestionGUIAdapter( itemId == $qid, suggestedControl == null )
then
    $qga.setSuggestedControl("checkbox");
end

rule "Recomm_DropDown"
salience -1  //override
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $q : MultipleChoiceQuestion( $qid : id, $qLab : preLabel, $req : required, $type : answerType,
                   singleAnswer == true,
                   numOfPossibleAnswers >= 6 )
    $qga : QuestionGUIAdapter( itemId == $qid )
then
    $qga.setSuggestedControl("dropdown");
end

rule "Recomm_MultiSelect"
salience -1  //override
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $q : MultipleChoiceQuestion( $qid : id, $qLab : preLabel, $req : required, $type : answerType,
                   singleAnswer == false,
                   numOfPossibleAnswers >= 6 )
    $qga : QuestionGUIAdapter( itemId == $qid )
then
    $qga.setSuggestedControl("combobox");
end






