package org.drools.informer.presentation;

import org.drools.informer.xml.*;
import org.drools.informer.*;
import org.drools.informer.MultipleChoiceQuestion.PossibleAnswer;


rule "Create_GUI_Adapter"
dialect "mvel"
no-loop
when
    $t : SurveyTicket( $sid : sid,
                       questionnaire != null,
                       $context : guiContext,
                       $inter : guiInteractive )
    $q : Questionnaire( id == $sid,
                   $lab : label,
                   $lang : defaultLanguage,
                   $type : type
                   )
    not SurveyGUIAdapter( itemId == $sid )
then
    SurveyGUIAdapter qa = new SurveyGUIAdapter();
        qa.itemId = $sid;
        qa.name = $lab;
        qa.language = $lang;
        qa.surveyClass = $type;
        qa.contextDesc = $context;
        qa.interactive = $inter;
        qa.surveyQuestions = new java.util.LinkedList();
    insert(qa);


    ChangeGUIAdapter cga = new ChangeGUIAdapter();
        cga.itemId = $sid;

        cga.newQuestions = new java.util.LinkedList();
        cga.deletedQuestions = new java.util.LinkedList();
        cga.updatedQuestions = new java.util.LinkedList();
    insert(cga);
end





rule "Add_GUI_Questions"

when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $q : Question( $qid : id, $qLab : preLabel, $post : postLabel, $req : required, $type : answerType, $qName : type )
    Questionnaire( id == $sid, itemList contains $qid )
    not QuestionGUIAdapter( itemId == $qid ) from $guiQuestions
then
    QuestionGUIAdapter qga = new QuestionGUIAdapter();
        qga.setItemId($qid);
        qga.setQuestionName($qName);
        qga.setPreLabel($qLab);
        qga.setRequired($req);
        qga.setCurrentAnswer( $q.getLastAnswer() );
        qga.setAnswerType($type.getValue());
        qga.setValidationMessage( $q.getPostLabel() );
    $sga.getSurveyQuestions().add(qga);
    insert(qga);
    update($sga);
end



rule "Valid_GUI_Questions"
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $q : Question( $qid : id, $qLab : preLabel, $post : postLabel, $req : required, $type : answerType, $qName : type, $last : lastAnswer != null )
    Questionnaire( id == $sid, itemList contains $qid )
    $qga : QuestionGUIAdapter( itemId == $qid )
    not InvalidAnswer( questionId == $qid )
then
    $qga.setSuccessType( "valid" );
end

rule "Missing_GUI_Questions"
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $q : Question( $qid : id, $qLab : preLabel, $post : postLabel, $req : required, $type : answerType, $qName : type, $last : lastAnswer == null )
    Questionnaire( id == $sid, itemList contains $qid )
    $qga : QuestionGUIAdapter( itemId == $qid )
    not InvalidAnswer( questionId == $qid )
then
    $qga.setSuccessType( "missing" );
end

rule "Invalid_GUI_Questions"
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $q : Question( $qid : id, $qLab : preLabel, $post : postLabel, $req : required, $type : answerType, $qName : type, $last : lastAnswer != null )
    Questionnaire( id == $sid, itemList contains $qid )
    $qga : QuestionGUIAdapter( itemId == $qid )
    InvalidAnswer( questionId == $qid )
then
    $qga.setSuccessType( "invalid" );
end







rule "BooleanQuestions"
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $q : Question( $qid : id, $qLab : preLabel, $post : postLabel, $req : required, $type : answerType == Question.QuestionType.TYPE_BOOLEAN )
    $qga : QuestionGUIAdapter( itemId == $qid, possibleAnswers == null)
then
    $qga.setPossibleAnswers( new java.util.ArrayList() );
    $qga.getPossibleAnswers().add( new PossibleAnswer("true", "Yes") );
    $qga.getPossibleAnswers().add( new PossibleAnswer("false", "No") );
    update($qga);
end




rule "Synch_GUI_Questions"

when
    $q   : Question( $qid : id, $qLab : preLabel, $post : postLabel, $req : required, $type : answerType, $qName : type )
    $qga : QuestionGUIAdapter( itemId == $qid )
then
    $qga.setCurrentAnswer( $q.getLastAnswer() );
    $qga.setValidationMessage( $q.getPostLabel() );
end

rule "Init_MultipleChoice"
salience 100
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $qst : MultipleChoiceQuestion( $qid : id, $poss : possibleAnswers, $single : singleAnswer )
           Questionnaire( id == $sid, itemList contains $qid )
    $qga : QuestionGUIAdapter( itemId == $qid, $possAnswers : possibleAnswers == null )
then
    $qga.setPossibleAnswers(new java.util.LinkedList());
    update($qga);
end




rule "Add_MultipleOptions"
when
    $sga : SurveyGUIAdapter( $sid : itemId, $guiQuestions : surveyQuestions)
    $qst : MultipleChoiceQuestion( $qid : id, $poss : possibleAnswers, $single : singleAnswer )
           Questionnaire( id == $sid, itemList contains $qid )
    $qga : QuestionGUIAdapter( itemId == $qid, $possAnswers : possibleAnswers != null )
    $pa  : MultipleChoiceQuestion.PossibleAnswer( $key : label, $val : value, this not memberOf $possAnswers ) from $poss
then
    $qga.setSingleAnswer(""+$single);
    // possibleAnswers are read-only, so let's just add them directly
    $possAnswers.add($pa);
end








rule "On Some Delete"
salience -1000
when
    $cga : ChangeGUIAdapter( $sid : itemId )
    $cc : ChangeCollector()
    Questionnaire( id == $sid )
    $xo : ItemId( $xid : id ) from $cc.getDelete()
then
    $cga.getDeletedQuestions().add($xid);
end


rule "On Some Change"
salience -1000
when
    $cga : ChangeGUIAdapter( $sid : itemId )
    $cc : ChangeCollector()
    Questionnaire( id == $sid )
    $xo : Question( $qid : id ) from $cc.getUpdate()
    $qga : QuestionGUIAdapter( itemId == $qid )
then
    $cga.getUpdatedQuestions().add($qga);
end

rule "On Some New"
salience -1000
when
    $cga : ChangeGUIAdapter( $sid : itemId )
    $cc : ChangeCollector()
    Questionnaire( id == $sid )
    $xo : Question( $qid : id ) from $cc.getCreateList()
    $sga : SurveyGUIAdapter( itemId == $sid)
    $qga : QuestionGUIAdapter( itemId == $qid ) from $sga.getSurveyQuestions()
then
    $cga.getNewQuestions().add($qga);
end


rule "Action_Request_setSurvey_init1"
salience 10
when
    ChangeCollector()
    $cga : ChangeGUIAdapter()
then
    $cga.getUpdatedQuestions().clear();
    $cga.getDeletedQuestions().clear();
    $cga.getNewQuestions().clear();
end