/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 *
 * Copyright 2011 JBoss Inc
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package org.drools.chance.test;

import org.drools.chance.factmodel.Imperfect;
import org.drools.chance.factmodel.LinguisticPartition;

import de.lab4inf.fuzzy.FuzzySet;
import de.lab4inf.fuzzy.polygons.FuzzyTriangle;

import org.drools.chance.degree.Degree;
import org.drools.chance.reteoo.ChanceActivation;


global java.util.Map map;

    /*

       SLIM      FAT
  1.0    | \     / |
         |   \ /   |
         |   / \   |
  0.0    | /     \ |
       --------------->
         0         100
    */


declare enum Weight
@LinguisticPartition

	SLIM( "slim", new de.lab4inf.fuzzy.polygons.FuzzyTriangle( -0.01, 0, 100 ) ),

	FAT( "fat", new de.lab4inf.fuzzy.polygons.FuzzyTriangle( 0, 100, 100.01 ) );

    label : String

    set   : FuzzySet

end


declare enum Age
@LinguisticPartition

	YOUNG( "young", new de.lab4inf.fuzzy.polygons.FuzzyTriangle( -0.01, 0, 100 ) ),

	OLD( "old", new de.lab4inf.fuzzy.polygons.FuzzyTriangle( 0, 100, 100.01 ) );

    label : String

    set   : FuzzySet

end


declare Imperson

	name    : String    = "john/0.3, philip/0.7"
	                @Imperfect( kind=ImpKind.PROBABILITY, type=ImpType.DISCRETE, degree=DegreeType.SIMPLE )

    body    : Weight
                    @Imperfect( kind=ImpKind.FUZZINESS, type=ImpType.LINGUISTIC, degree=DegreeType.SIMPLE, support="weight" )

    age     : Age
                    @Imperfect( kind=ImpKind.FUZZINESS, type=ImpType.LINGUISTIC, degree=DegreeType.SIMPLE, support="years" )

    weight  : Double    = 65.0

    years   : Double    = 25.0

end




rule "Init"
when
then
    insert( new Imperson( ) );
end

rule "Fuzzy Rule : Distribution input"
salience 100
when
    $b : Imperson( name == "philip",
                   body ~is Weight.FAT && (                 // 0.65
                        body ~is Weight.SLIM                // 0.5
                        ||
                        age ~is Age.OLD ),                  // 0.5
                   age ~is Age.YOUNG )                      // 0.75
            @Imperfect( family=MvlFamilies.GODEL, degree=DegreeType.SIMPLE )
then
    Degree x = chance.getDegree();
    System.out.println( "X " + x );  // 0.5
    map.put( "X", x.getValue() );
end



rule "Fuzzy Rule : Number input"
when
    $b : Imperson( name == "philip",
                   weight ~is Weight.FAT && (               // 0.65
                        weight ~is Weight.SLIM              // 0.35
                        ||
                        years ~is Age.OLD ),                // 0.25
                   years ~is Age.YOUNG )                    // 0.75
            @Imperfect( family=MvlFamilies.GODEL, degree=DegreeType.SIMPLE )
then
    Degree x = chance.getDegree();
    System.out.println( "Z " + x );  // 0.35
    map.put( "Z", x.getValue() );

end


rule "Fuzzy Rule : Crisp Set input"
salience 50
when
    $b : Imperson( name == "philip",
                   bodyValue ~is Weight.FAT && (            // 1.0
                        bodyValue ~is Weight.SLIM           // 0.5
                        ||
                        ageValue ~is Age.OLD ),             // 0.5
                   ageValue ~is Age.YOUNG )                 // 1.0
            @Imperfect( family=MvlFamilies.GODEL, degree=DegreeType.SIMPLE )
then
    Degree x = chance.getDegree();
    System.out.println( "Y " + x );  // 0.5
    map.put( "Y", x.getValue() );
end
